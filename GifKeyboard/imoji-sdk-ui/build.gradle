apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultPublishConfig "release"

    defaultConfig {
        minSdkVersion 12
        targetSdkVersion 25
        versionCode 1
        versionName "2.1.6"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23+'
    compile('io.imoji.sdk:imoji-sdk:+@aar') {
        transitive = true
    }
    compile 'com.android.support:cardview-v7:23+'
    compile 'com.android.support:recyclerview-v7:23+'
    compile 'com.android.support:gridlayout-v7:23+'
    compile 'com.felipecsl:gifimageview:2.0.0'
    compile 'com.android.support:design:23+'
}

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")
signing {
    required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
//            authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
//            authentication(userName: ossrhUsername, password: ossrhPassword)
        }

        pom.project {
            groupId 'io.imoji.sdk'
            artifactId 'imoji-sdk-ui'
            version android.defaultConfig.versionName
            name 'io.imoji.sdk:imoji-sdk-ui'
            description 'Integrate millions of stickers into your Android application'
            url 'https://imoji.io/developers'
            licenses {
                license {
                    name 'MIT License'
                    url 'https://raw.githubusercontent.com/imojiengineering/imoji-android-sdk-ui/master/LICENSE.md'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    name 'Sajjad Tabib'
                    email 'sajjad@imojiapp.com'
                    url 'https://github.com/stabib'
                    organization 'Builds, Inc'
                    organizationUrl 'https://imoji.io'
                }
                developer {
                    name 'Engin Dogusoy'
                    email 'engin.dogusoy@gmail.com'
                    url 'https://github.com/engd'
                    organization 'Builds, Inc'
                    organizationUrl 'https://imoji.io'
                }
                developer {
                    name 'Nima Khoshini'
                    email 'nima@imojiapp.com'
                    url 'https://github.com/nkhoshini'
                    organization 'Builds, Inc'
                    organizationUrl 'https://imoji.io'
                }
            }
            scm {
                connection 'scm:git:git@github.com:imojiengineering/imoji-android-sdk-ui.git'
                developerConnection 'scm:git:git@github.com:imojiengineering/imoji-android-sdk-ui.git'
                url 'git@github.com:imojiengineering/imoji-android-sdk-ui.git'
            }
        }
    }
}